buildscript {
    apply from: '../../version.gradle'
}

plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 30
        versionCode project.versionCode
        versionName project.versionName
        buildConfigField("String", "SDK_VERSION", "\"" + project.versionName + "\"")
        buildConfigField("String", 'CLOUD_ENDPOINT_PRODUCTION', "\"https://jacquard.googleapis.com\"")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
        exclude 'META-INF/gradle/incremental.annotation.processors'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/INDEX.LIST'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.auto.value:auto-value-annotations:1.7.4'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'com.google.guava:guava:30.1-android'
    implementation 'com.google.protobuf:protobuf-java:3.21.1'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
    implementation 'com.ryanharter.auto.value:auto-value-gson-runtime:1.3.1'
    annotationProcessor 'com.google.auto.value:auto-value:1.7.4'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-gson-factory:1.3.1'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-gson-extension:1.3.1'
    testImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation 'com.google.truth:truth:1.1.2'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.4'
    testImplementation 'org.mockito:mockito-core:3.7.7'
}

task generateMavenArtifacts(type :Exec) {
    commandLine './generateMavenArtifacts.sh'
    args project.versionName
}

// Maven publishing

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

project.afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'com.google.jacquard'
                artifactId = 'jacquard-sdk'
                version = project.versionName
                artifact sourcesJar
                artifact bundleReleaseAar
                pom.withXml {
                    def licensesNode = asNode().appendNode('licenses')
                    addLicense(licensesNode)

                    def dependenciesNode = asNode().appendNode('dependencies')
                    project.configurations.implementation.allDependencies.each {
                        addDependency(dependenciesNode, it)
                    }
                    project.configurations.annotationProcessor.allDependencies.each {
                        addDependency(dependenciesNode, it)
                    }
                }
            }
        }
    }
}

private void addLicense(licensesNode) {
    def licenseNode = licensesNode.appendNode('license')
    licenseNode.appendNode('name', 'The Apache License, Version 2.0')
    licenseNode.appendNode('url', 'https://www.apache.org/licenses/LICENSE-2.0.txt')
}

private void addDependency(dependenciesNode, it) {
    if (it.group?.trim()) { // ignore if group is empty
        def dependencyNode = dependenciesNode.appendNode('dependency')
        dependencyNode.appendNode('groupId', it.group)
        dependencyNode.appendNode('artifactId', it.name)
        dependencyNode.appendNode('version', it.version)
    }
}

