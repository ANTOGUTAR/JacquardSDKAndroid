buildscript {
    apply from: '../../version.gradle'
}

plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 30
        versionCode project.versionCode
        versionName project.versionName
        buildConfigField("String", "SDK_VERSION", "\"" + project.versionName + "\"")
        buildConfigField("String", 'CLOUD_ENDPOINT_PRODUCTION', "\"https://jacquard.googleapis.com\"")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/INDEX.LIST'
    }
}

dependencies {
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
    // https://mvnrepository.com/artifact/com.google.auto.value/auto-value
    annotationProcessor group: 'com.google.auto.value', name: 'auto-value', version: '1.7.4'
    // https://mvnrepository.com/artifact/com.google.auto.value/auto-value-annotations
    implementation group: 'com.google.auto.value', name: 'auto-value-annotations', version: '1.7.4'
    annotationProcessor("com.ryanharter.auto.value:auto-value-gson-factory:1.3.1")
    annotationProcessor("com.ryanharter.auto.value:auto-value-gson-extension:1.3.1")
    implementation("com.ryanharter.auto.value:auto-value-gson-runtime:1.3.1")
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.7'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.google.truth:truth:1.1.2"
    testImplementation 'org.robolectric:robolectric:4.4'
    testImplementation 'org.mockito:mockito-core:3.7.7'
    testImplementation 'androidx.test.ext:junit:1.1.3'
    implementation group: 'com.google.guava', name: 'guava', version: '30.1-android'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
}

task generateMavenArtifacts(type :Exec) {
    commandLine './generateMavenArtifacts.sh'
    args project.versionName
}

// Maven publishing

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

project.afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'com.google.jacquard'
                artifactId = 'jacquard-sdk'
                version = project.versionName
                artifact sourcesJar
                artifact bundleReleaseAar
                pom {
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                }
            }
        }
    }
}

